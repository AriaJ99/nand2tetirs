//Implements a Flappy Bird game
class FlappyBird{
    field Bird bird; //bird of the game
    field Array pillars; //moving pillars
    field Pillar pillar;
    field int score;
    field int jump;
    field int y_limit;
 
    field int x_limit;

    constructor FlappyBird new(){
        //var int 
        let pillars= Array.new(4);
        let y_limit=255;
        let x_limit=511;
        let score=0;
        let jump=10;
        let bird=Bird.new(254,127);

        let pillars[3]=Pillar.new(441);
        let pillars[2]=Pillar.new(318);
        let pillars[1]=Pillar.new(205);
        let pillars[0]=Pillar.new(72);
        return this;
    }
    method void reset(){
        var int cnt;
        
        let score=0;
        let cnt=4;
        while(cnt>0){
            let cnt=cnt-1;
            let pillar=pillars[cnt];
            do pillar.dispose();
        }
        let pillars[3]=Pillar.new(441);
        let pillars[2]=Pillar.new(318);
        let pillars[1]=Pillar.new(205);
        let pillars[0]=Pillar.new(72);
        do bird.dispose();
        let bird=Bird.new(254,127);
        do draw();
        return;

    }
    method void draw(){
        var int cnt;
        let cnt=4;
        do Screen.clearScreen();
        do bird.draw(); 
        while(cnt>0){
            let cnt=cnt-1;
            let pillar=pillars[cnt];
            do pillar.draw();}
        return;
    }
    method void start(){
        var int key;
        let key=0;
        while(key=0){
            let key=Keyboard.keyPressed();
        }
        while(~(key=0)){
            let key=Keyboard.keyPressed();
        }
        return;
    }
    method void run(){
        var int key;
        var boolean exit,pause;
        var int jump_token;//a token to keep track of how much jump is left to perform
        var int pillar_move_cnt;//number of cycles for pillar to move forward
        var int bird_move_cnt;//number of cyclees for bird to move
        var boolean game_over;
        var int cnt; //loop counter
        let key=0;
        let exit=false;
        let pause=false;
        let jump_token=0;
        let pillar_move_cnt=0;
        let bird_move_cnt=0;
        let cnt=4;
        //clean up the screen and draw figures
        
        do draw();
        let cnt=4;
        do start();
        while(~exit){

            //do Screen.setColor(false);

            if(pillar_move_cnt=0){
                let cnt=4;
            while(cnt>0){
                let cnt=cnt-1;
                let pillar=pillars[cnt];
                do pillar.move_forward();
                //do pillar.set_x(pillar.get_x()-1);// pillar moving forward
                if(pillar.get_x()=0){
                    do pillar.dispose();
                    let pillar=Pillar.new(491);
                }
            }
            let pillar_move_cnt=40;
            }
            let pillar_move_cnt=pillar_move_cnt-1;
            if(bird_move_cnt=0){
            if(jump_token>0){
                    //bird flying up
                    //check if bird has reached the highest point of the screen
                    if(bird.get_y()>0){
                        do bird.move_up();
                        //do bird.set_y(bird.get_y()-1);
                        let jump_token=jump_token-1;
                    }
                    else{
                        let jump_token=0;
                    }
                }
                else{
                    //do bird.set_y(bird.get_y()+1);
                    do bird.move_down();
                }
                let bird_move_cnt=25;
            }
            let bird_move_cnt=bird_move_cnt-1;
            //checking if the game is over(by hitting the pillar or ground)
            let game_over=false;
            let game_over=check_collision(bird, pillars,4);//check if the bird has hit the pillar
            if(bird.get_y()+bird.get_thickness()=y_limit){

                let game_over=true;
            }
            if(game_over){
                //if collision happens or bird hit the ground resets the game
                do start();
                do reset();
                do start();
                //let pause=true;

                let jump_token=0;

            }
            if(~game_over){
            let key=Keyboard.keyPressed();
            if(~(key=0)){
                if(key=81){
                    //q key 
                    //exit 
                    let exit=true;
                }
                if(key=80){
                    //p key pause

                    let exit=pause();
                    }           
                
                
                if(key=32){
                    //space key
                    //jump
                    if(jump_token=0){
                        let jump_token=20;
                    }
                    
                   
                }
            }
             while(~(key=0)){
                        let key=Keyboard.keyPressed();
                    }
            }
            else{
                let game_over=false;
                
            }
            
        }
    return;
    }
    method boolean check_collision(Bird bird, Array pillars, int length){
        //compare the lowest and highest points of the bird and pillar opening to see if collision occured
        var int p_x_lower_bound,p_x_upper_bound,b_x_lower_bound,b_x_upper_bound;
        var int p_y_lower_bound,p_y_upper_bound,b_y_lower_bound,b_y_upper_bound;
        var int cnt;
        var boolean ans;
        let cnt=length;
        let ans=false;
        while(cnt>0){
        //assign x boundaries
        let cnt=cnt-1;
        let pillar=pillars[cnt];
        let p_x_upper_bound=pillar.get_x()+pillar.get_width();
        let p_x_lower_bound=pillar.get_x();
        let b_x_lower_bound=bird.get_x();
        let b_x_upper_bound=bird.get_x()+bird.get_length();
        //assign y boundaries
        let p_y_upper_bound=pillar.get_upper_edge();
        let p_y_lower_bound=pillar.get_lower_edge();
        let b_y_lower_bound=bird.get_y()+bird.get_thickness();
        let b_y_upper_bound=bird.get_y();
        //checking y boundaries
        if((b_x_upper_bound>p_x_lower_bound) & (b_x_lower_bound<p_x_upper_bound)){
        if(b_y_upper_bound<p_y_upper_bound){
            let ans=true;
        }
        if(b_y_lower_bound>p_y_lower_bound){
            let ans=true;
        }
        }
        }
        return ans;

    }
    method boolean pause(){
        var int key;
        var boolean exit;
        let exit=false;
        let key=1;
        //we have to wait for key to be released since we are calling a function here
       
        while(~(key=0)){
            let key=Keyboard.keyPressed();
        }
        while(key=0){
            let key=Keyboard.keyPressed();
            if(key=81){
                //q key 
                //exit 
                let exit=true;
            }   

            else {
                // only if space or pause pressed continue playing
                if((key=80) | (key=32)){
                    let key=key;
                }
                else{
                    let key=0;
                }
                
            }
        }
        return exit;
    }
    method void dispose(){
        do bird.dispose();
        do pillar.dispose();
        do Memory.deAlloc(this);
        return;
    }


}